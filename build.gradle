import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "java"
    id "com.gradleup.shadow" version "8.3.4"
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

group = "moe.minacle.minecraft"
version = "1.0.5"

repositories {
    mavenCentral()
    maven {
        name "PaperMC"
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name "Sonatype"
        url "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name "Sonatype"
        url "https://s01.oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT"
    implementation "org.bstats:bstats-bukkit:3.1.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("paper-plugin.yml") {
        expand props
    }
}

tasks {
    runServer {
        minecraftVersion "1.21.3"
    }
}

tasks.named("shadowJar", ShadowJar) {
    enableRelocation true
    relocationPrefix "moe.minacle.minecraft.plugins.hameln.shadowjar"
}

shadowJar {
    archiveClassifier.set("")
    minimize()
}
